plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.8.18'
}

group 'com.oddin.ghost'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

def protobufVersion = "3.17.2"
def grpcVersion = "1.42.1"

dependencies {
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+
}

sourceSets {
  main {
    proto {
        srcDir '../proto/'
    }
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
    // artifact = "com.google.protobuf:protoc:$protobufVersion:osx-x86_64" // !!! Important: uncomment this if you are using Apple M1 chip
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'library'
            version = '0.1'
            from components.java
        }
    }
}

task install(dependsOn: publishToMavenLocal)
